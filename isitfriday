#!/usr/bin/env perl
# isitfriday
# checks if the day is friday, and if it is, returns exit status successful
# Copyright (C) 2016, 2017  Pär Karlsson
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict;
use warnings;
use utf8;
use DateTime;
use Readonly;
use Getopt::Long;
use Pod::Usage;
use Term::ANSIColor;

Readonly my $FRIDAY => 5;

sub evaluate_fridayness {
    my $f = shift;
    print "F = $f\n";
    if ( $f == 0 ) {
        print "Fridayness is zero. It's not friday at all.\n";
    }
    if ( $f > 0 && $f <= 0.25 ) {
        print
"There's some fridayness in the air. The fragrance of friday is discernible.\n";
    }
    if ( $f > 0.25 && $f <= 0.5 ) {
        print "Friday is close, but still less friday than any other day.\n";
    }
    if ( $f > 0.5 && $f <= 0.75 ) {
        print "We're definitely in the friday zone. Fridayness is above 0.5\n";
    }
    if ( $f > 0.75 && $f < 1 ) {
        print "Gosh golly, it's so much friday it's fridayning.\n";
    }
    if ( $f == 1 ) {
        print "THANK GOD IT'S FRIDAY!!!1!11!!!!111!!\n";
    }
}

sub main {
    my $verbose  = 0;
    my $all      = 0;
    my $help     = 0;
    my $time     = 0;
    my $evaluate = 0;
    my $index    = 0;

    Getopt::Long::Configure ('bundling');

    GetOptions(
        'all|a'      => \$all,
        'verbose|v'  => \$verbose,
        'help|h'     => \$help,
        'time|t'     => \$time,
        'evaluate|e' => \$evaluate,
        'index|i'    => \$index,
    ) or pod2usage(2);

    if ($help) {
        pod2usage( -exitval => 0, -verbose => 2 );
    }

    # this date is UTC
    my $now = DateTime->now( time_zone => 'local' );

    my $fridays = 0;
    my $entries = 0;
    my $retval  = 1;

    if ($all) {
        for my $zone ( DateTime::TimeZone->all_names() ) {
            $entries++;
            $now->set_time_zone($zone);
            if ( $verbose ) {
                print "$zone: ";
            }
            if ( $now->day_of_week() == $FRIDAY ) {
                $fridays++;
                if ($verbose) {
                    print color 'green';
                    print 'YES';
                    print color 'reset';
                    if ($time) {
                        print " ($now)";
                    }
                }
            }
            else {
                if ($verbose) {
                    print color 'red';
                    print 'NO';
                    print color 'reset';
                    print q{ (} . $now->day_name();
                    if ($time) {
                        print ", $now)";
                    }
                    else {
                        print q{)};
                    }
                }
                elsif ($time) {
                    print " ($now)";
                }
            }
            if ($verbose) {
                print "\n";
            }
        }
    }
    else {
        $entries++;
    }

    if ( $now->day_of_week() == $FRIDAY ) {
        $fridays++;
        if ($verbose) {
            print color 'green';
            print 'YES :-D';
            print color 'reset';
            if ($time) {
                print " $now";
            }
            print "\n";
        }
        elsif ($time) {
            print "$now\n";
        }
        $retval = 0;
    }
    else {
        if ($verbose) {
            print color 'red';
            print 'NO ;-(';
            print color 'reset';
        }
        if ( $verbose && $time ) {
            print q{ };
        }
        if ($time) {
            print "($now)";
        }
        if ( $verbose || $time ) {
            print "\n";
        }
    }
    if ($evaluate) {
        evaluate_fridayness( $fridays / $entries );
    }
    if ($index) {
        print $fridays / $entries . "\n";
    }

    return $retval;
}

exit main(@ARGV);

__END__

=encoding utf8

=head1 MANE

isitfriday - checks if it is friday right now, and returns POSIX exit value
true (0) if it is friday, exit value false (1) if it is any other day

=head1 SYMPOSIA

isitfriday [options]

  Options (explanations provided by Google Search completion)
    --help,     -h  Help me, I'm poor
    --all,      -a  All about that bass
    --time,     -t  Times of India
    --evaluate, -e  Evaluate the integral
    --index,    -i  Index funds
    --verbose,  -v  Verbose in Unix

=head1 OPTICS

=over 4

=item B<--help, -h>

Print a brief help message and exit

=item B<--all, -a>

What about the rest of the world, how are they doing?

=item B<--time, -t>

Print out the actual time

=item B<--evaluate, -e>

Objectively speaking, how much friday is it? F is the fridayness ratio.
Combined with -a it shows the global friday index (FI), without -a it shows
the local FI

=item B<--index, -i>

Only output the friday index on a row by itself (suitable for graphing)

=item B<--verbose, -v>

Be more explicit about what day it is (should not be needed by real Unix
admins).

=back

=head1 INSCRIPTION

B<isitfriday> will output the proper answer to whether it is friday or not,
readable by humans, machines and cyborgs alike.

=head1 EXSTATIC US

The exit status is 0 if it is friday (in your locale). 1 if it is any other
day.

=head1 DICTATOR

Written by Pär Karlsson <par.j.karlsson@telenor.com>

=cut
